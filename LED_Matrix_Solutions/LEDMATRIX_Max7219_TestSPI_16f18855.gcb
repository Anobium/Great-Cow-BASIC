''' A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
''' This program tests the SPI connections, this uses the serial port to display information on an ANSI serial terminal - use PUUTY in your Great Cow BASIC Installation.
''' You may not need PPS for your specific chip - simply remove if not required.
''':
'''@author  Evan R. Venn
'''@licence GPL
'''@version 1.00
'''@date    27.01.2017
'''********************************************************************************
' ----- Configuration
#CHIP 16f18855,32
#CONFIG MCLRE=On
#OPTION Explicit

#STARTUP InitPPS, 85
Sub InitPPS

    UNLOCKPPS
    'Module: EUSART
    'TX > RC0
    RC0PPS = 0x0010
    'RC0 > TX (bi-directional)
    TXPPS = 0x0008
    #IFDEF MAX7219_LEDMatrix_HardwareSPI
        'Module: SPI
        '   //RB4->MSSP1:SDI1;
        SSP1DATPPS = 0x0C
        '   //RB5->MSSP1:SDO1;
        RB5PPS = 0x15
        '   //RB3->MSSP1:SCK1;
        RB3PPS = 0x14
    #ENDIF
    LOCKPPS

End Sub




' ----- Constants
'Set SPI pins for the Max7219
#DEFINE Max7219_DI    portb.4
#DEFINE Max7219_DO    portb.5
#DEFINE Max7219_SCK   portb.3
#DEFINE Max7219_CS    portc.3

#INCLUDE <lcd2serialredirect.h>
' ----- Define Hardware settings
' Required to LCD to Serial redirection
#DEFINE LCD_IO 0

' USART settings
#DEFINE USART_BAUD_RATE 19200
' just none OR one of the blocking
#DEFINE USART_BLOCKING
' statements should be defined
#DEFINE USART_TX_BLOCKING
#DEFINE USART_DELAY 2 ms

CLS
Dim debugypos As Byte
debugypos = 1

Wait 1 s
Locate debugypos, 0
InCrementScreen
Print "Test SPI Only"
Locate debugypos, 0
InCrementScreen
Print "Sending 0 - 255 to SPI DOUT"
Locate debugypos, 0
InCrementScreen
Print "Check with Analyzer"
Locate debugypos, 0
InCrementScreen
Print "You should see - data 0 to 255"

'initialise variable
Dim testbyteOut, testbyteNull As Byte
testbyteOut = 0

'init SPI
SPIMode masterfast, SPI_CPOL_0 + SPI_CPHA_0
'We are only using three lines
Dir Max7219_DO Out
Dir Max7219_SCK Out
Dir Max7219_CS Out

Do

    'Test Chip Select lines
    Max7219_CS = 0
    'Send data
    SPITransfer testbyteOut, testbyteNull
    Max7219_CS = 1
    testbyteOut++

Loop




Sub InCrementScreen
    debugypos++
    If debugypos > 43 Then
        HSerPrintCRLF
    End If
End Sub
