'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program shows how to change the brightness of the LED Matrix
''':
'''You must select the correct Pset constant to match your LED Matrix.  See Configuration below.
''':
''':
'''@author  Evan R. Venn
'''@licence GPL
'''@version 1.00
'''@date    27.01.2017
'''********************************************************************************
' ----- Configuration
#CHIP 16f18855,32
#CONFIG MCLRE=On
#INCLUDE <max7219_ledmatrix_driver.h>
#OPTION Explicit

#STARTUP InitPPS, 85
Sub InitPPS
    #IFDEF MAX7219_LEDMatrix_HardwareSPI
        UNLOCKPPS
        '   //RB4->MSSP1:SDI1;
        SSP1DATPPS = 0x0C
        '   //RB5->MSSP1:SDO1;
        RB5PPS = 0x15
        '   //RB3->MSSP1:SCK1;
        RB3PPS = 0x14
        LOCKPPS
    #ENDIF
End Sub


' ----- Constants
'Set SPI pins for the Max7219
#DEFINE Max7219_DI    portb.4
#DEFINE Max7219_DO    portb.5
#DEFINE Max7219_SCK   portb.3
#DEFINE Max7219_CS    portc.3

'Specifics for device configuration
#DEFINE MAX7219_X_Devices 4
' remove and make a comment out if you want to use Software SPI for this device
#DEFINE MAX7219_LEDMatrix_HardwareSPI

'Choose one of the constants below to match the orientation of your LED Matrixes.
'All the LED Matrixes must have the same orientation.
#DEFINE Pset max7219_LEDMatrix_PSet_0
'#define Pset max7219_LEDMatrix_PSet_90
'#define Pset max7219_LEDMatrix_PSet_180
'#define Pset max7219_LEDMatrix_PSet_270

'; ----- Main body of program commences here - cycle thru power settings


Dim myString As String  * 5
Dim myLEDPower As Byte
myLEDPower = 0


Do Forever

    GLCDCLS
    Max7219_LEDMatrix_Brightness ( myLEDPower )
    GLCDPrint ( 0, 0, "B@ "+Str(myLEDPower) )

    'Display
    Max7219_LEDMatrix_Update
    myLEDPower++

    'Max Max7219_LEDMatrix_Brightness is 15
    If myLEDPower = 16 Then
        myLEDPower = 0
    End If

    Wait 500 ms

Loop

End
