'''A demonstration program for GCGB and GCB.
'''------------------------------------------------------------------------------------------------------------------------------
''' Description: this library provides an ISR to implement
''' a stateful i2c hardware slave.
''' This is a GCB implementation of Microchip Application Note AN734.
'''
'''
''' This demonstration responds to a write of 4 bytes to I2C address 0x4C (which is really Read Address 0x4D)
''' This example requires two pots and terminal to show the results.
'''
'''
''' According to AN734, there are 5 possible i2c states. During ISR, each
''' of this states are detected. This ISR provides a standard skeleton to implement
''' an i2c hardware slaves, while client code must implement several callbacks
''' the ISR is expecting to call while processing states.
'''
''' Callbacks:
''' - HI2C_Process_In_Message ( in HI2CMESSAGESIZE as byte )
'''      called when slave has a full buffer
'''
''' - HI2C_Process_Out_Message
'''      called when slave is requested to respond to a masters request
'''
''' - You can redirect any of the standard callbacks like HI2CSlave_State_Error, HI2CSlave_State_1-5
'''   Use a define to call your own specific routine. like #define HI2CSlave_State_Error  MyHI2CSlave_State_Error
'''
'''
'''

'''@author  EvanV
'''@licence GPL
'''@version 0.9b
'''@date    02.11.2015
'''********************************************************************************

' ---'''Configuration

#CHIP 16f18855,32
#OPTION Explicit


'Set the PPS of the I2C and the RS232 ports.
#STARTUP InitPPS, 85
Sub InitPPS
    'RC0->EUSART:TX;
    RC0PPS = 0x0010
    'RC1->EUSART:RX;
    RXPPS  = 0x0011

    'RC3->MSSP1:SCL1;
    SSP1CLKPPS = 0x14
    'RC4->MSSP1:SDA1;
    SSP1DATPPS = 0x13
    'RC3->MSSP1:SCL1;
    RC3PPS = 0x15
    'RC4->MSSP1:SDA1;
    RC4PPS = 0x14

End Sub

'defines a set of callbacks - you do not need to define hi2cslave_state_1-5
#INCLUDE <hwi2c_messageinterface.h>

' redirect standard Error handler to my handler
#DEFINE HI2CSlave_State_Error  MyHI2CSlave_State_Error

'Buffer size
#DEFINE HI2CBUFFERSIZE 16

' error messaging LEDs
#DEFINE HI2CSlaveSSPOVOverflowErrorLED    porta.0
#DEFINE HI2CSlaveStateNotHandledErrorLED  porta.1

' set up alert LEDs
#IFDEF HI2CSlaveStateNotHandledErrorLED
    Dir HI2CSlaveStateNotHandledErrorLED Out
#ENDIF
#IFDEF HI2CSlaveSSPOVOverflowErrorLED
    Dir HI2CSlaveSSPOVOverflowErrorLED Out
#ENDIF

' Required I2C settings - CHANGE PORTS if required
#DEFINE hi2c_BAUD_RATE 400
#DEFINE hi2c_DATA PORTC.4
#DEFINE hi2c_CLOCK PORTC.3
'Initialise I2C Master
'I2C pins need to be input for SSP2 module
Dir hi2c_DATA In
Dir hi2c_CLOCK In

HI2CSetAddress 0x40
HI2CMode Slave
HI2CSlave_ISR_Init

On Interrupt  SSP1Ready Call HI2CSlave_ISR_Handler


' THIS CONFIG OF THE SERIAL PORT WORKS WITH A  MAX232 (or equiv) THEN TO PC
' USART settings - CHANGE PORTS if required
#DEFINE USART_BAUD_RATE 19200
#DEFINE USART_TX_BLOCKING

' ---'''Define Hardware settings

Dir porta.0 Out
Dir porta.1 Out
Dir porta.2 Out
Dir porta.3 Out

' ---'''Variables
Dim HI2CForLoop As Byte


' ---'''Main body of program commences here.

Wait 100 ms
HSerPrint "Demo of HWI2C_ISR_Slave_LEDControl_Demov0_9a.gcb"
HSerPrintCRLF

HSerPrint ChipNameStr
HSerPrintCRLF

Repeat 20

    porta.0 = !porta.0
    porta.1 = !porta.1
    porta.2 = !porta.2
    porta.3 = !porta.3
    Wait 100 ms


End Repeat

Do Forever

    'This terminal output WILL cause timing issues. This NOT a Recommend method to show the data stream - use HserSend in HEX!

    'Do stuff
Loop

Sub HI2C_Process_In_Message ( In HI2CMESSAGESIZE )

    'We have data!!  Just send some the First byte back
    Select Case HI2CBUFFER(0)

    Case 0x23
        If (HI2CMESSAGESIZE = 2) Then
            porta = HI2CBUFFER(1)
        End If

    End Select

End Sub


Sub HI2C_Process_Out_Message
    'Want to post process the data? do it here.

End Sub

#DEFINE HI2CSlave_State_Error  MyHI2CSlave_State_Error

Sub MyHI2CSlave_State_Error ( In HI2CErrorCode )

    Dim I2Ctemp As Byte
    ' HI2CErrorCode = What caused the error...
    ' SSPSTAT will tell you the status
    'Recommend to consume the Buffer
    Do While  BF = 1
        I2Ctemp = SSPBUF
    Loop

    Select Case HI2CErrorCode
        #IFDEF HI2CSlaveSSPOVOverflowErrorLED
        Case HI2CSlaveSSPOVOverflowError
            Dir HI2CSlaveSSPOVOverflowErrorLED Out
            HI2CSlaveSSPOVOverflowErrorLED = !HI2CSlaveSSPOVOverflowErrorLED
        #ENDIF
        #IFDEF HI2CSlaveStateNotHandledErrorLED
        Case HI2CSlaveStateNotHandledError
            Dir HI2CSlaveStateNotHandledErrorLED Out
            HI2CSlaveStateNotHandledErrorLED = !HI2CSlaveStateNotHandledErrorLED
        #ENDIF
    End Select

End Sub
