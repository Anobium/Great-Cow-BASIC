'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program searches for I2C devices and display their adresses to the attached PC terminal.
'''This program uses the software implementation of I2C for the Microchip microprocessors.
'''The hardware serial connections are shown in the program.
'''@author  EvanV
'''@licence GPL
'''@version 1.0a
'''@date    14.12.15
'''********************************************************************************

' ----- Configuration
#CHIP 16F690
#OPTION Explicit

' ----- Define Hardware settings
' Define I2C settings - CHANGE PORTS
#DEFINE I2C_MODE Master
#DEFINE I2C_DATA PORTb.4
#DEFINE I2C_CLOCK PORTb.6
#DEFINE I2C_DISABLE_INTERRUPTS On

' THIS CONFIG OF THE SERIAL PORT WORKS WITH A  MAX232 (or equiv) THEN TO PC
' USART settings - CHANGE PORTS if required
#DEFINE USART_BAUD_RATE 19200
Dir PORTc.6 Out
Dir PORTc.7 In
#DEFINE USART_TX_BLOCKING
Wait 500 ms

'----- Variables
Dim DISPLAYNEWLINE As Byte


' ----- Main body of program commences here.
' Now assumes Serial Terminal is operational
Dim DeviceID As Byte

'  HSerPrintCRLF
'  dim myMessage as String * 12
'  myMessage = "Software I2C"
'  HSerPrint myMessage
'  HSerPrintCRLF 2

HSerPrintCRLF
HSerPrint "Software I2C   Live Demo"
HSerPrintCRLF 2

HSerPrint "     "
For DeviceID = 0 To 15
    HSerPrint Hex(deviceID)
    HSerPrint " "
Next
For DeviceID = 0 To 255
    DisplayNewLine = DeviceID % 16
    If DisplayNewLine = 0 Then
        HSerPrintCRLF
        HSerPrint Hex(DeviceID)
        HSerPrint ": "
    End If
    HSerPrint " "
    I2CStart
    I2CSend ( deviceID )

    If I2CSendState = TRUE  Then
        HSerPrint   Hex(deviceID)
    Else
        HSerPrint "--"
    End If
    I2CSend ( 0 )
    I2CStop


Next
HSerPrintCRLF 2
HSerPrint   "End of Search"
HSerPrintCRLF 2
End

