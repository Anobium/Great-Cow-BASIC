
#CHIP 18f45k50, 16
#OPTION Explicit


' Define I2C settings - CHANGE PORTS
#DEFINE I2C_MODE Master
#DEFINE I2C_DATA PORTb.0
#DEFINE I2C_CLOCK PORTb.1
#DEFINE I2C_DISABLE_INTERRUPTS On

' THIS CONFIG OF THE SERIAL PORT WORKS WITH max232 THEN TO PC
' USART settings
#DEFINE USART_BAUD_RATE 9600
Dir PORTb.7 Out
#DEFINE USART_DELAY 1 ms
#DEFINE USART_TX_BLOCKING
Wait 500 ms

'----- Variables
Dim DISPLAYNEWLINE As Byte


' ----- Main body of program commences here.
' Now assumes Serial Terminal is operational
Dim DeviceID As Byte

HSerPrintCRLF
HSerPrint "Software I2C "
HSerPrintCRLF 2

HSerPrint "     "
For DeviceID = 0 To 15
    HSerPrint Hex(deviceID)
    HSerPrint " "
Next
For DeviceID = 0 To 255
    DisplayNewLine = DeviceID % 16
    If DisplayNewLine = 0 Then
        HSerPrintCRLF
        HSerPrint Hex(DeviceID)
        HSerPrint ": "
    End If
    HSerPrint " "
    I2CStart
    I2CSend ( deviceID )

    If I2CSendState = TRUE  Then
        HSerPrint   Hex(deviceID)
    Else
        HSerPrint "--"
    End If
    I2CSend ( 0 )
    I2CStop


Next
HSerPrintCRLF 2
HSerPrint   "End of Search"
HSerPrintCRLF 2
End






