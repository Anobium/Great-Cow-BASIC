'''
'''******************************************************************
''' Serial connected to B.6 AT 19200 bps to a PC terminal
''' Input switch on A.5 pulled up with 10k
''' I2C SDA on B.5
''' I2C SCL on B.7
'''
'''  PIC: 16f15376
'''  Compiler: GCB
'''  IDE: GCB@SYN
'''
'''  Date: 19.12.2018
'''

'Example discovering PCF8563 Clock
'
'Hardware I2C
'
'
'   00 01 02 03 04 05 06 07 08 09 0A 0B 0C 0D 0E 0F
'00 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'10 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'20 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'30 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'40 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'50 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'60 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'70 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'80 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'90 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'A0 -- -- A2 A3 -- -- -- -- -- -- -- -- -- -- -- --
'B0 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'C0 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'D0 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'E0 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'F0 -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
'
'End of Search


' ----- Configuration
'Chip Settings.
#CHIP 16f15376, 32
#OPTION Explicit


'Generated by PIC PPS Tool for Great Cow Basic
'PPS Tool version: 0.0.5.22
'PinManager data: Not available (3)
'Generated for 16F15376
'
'Template comment at the start of the config file
'
#STARTUP InitPPS, 85
#DEFINE PPSToolPart 16F15376

Sub InitPPS

    'Module: MSSP1
    'SDA1 > RB5
    RB5PPS = 0x0016
    'RB5 > SDA1 (bi-directional)
    SSP1DATPPS = 0x000D
    'SCL1 > RB7
    RB7PPS = 0x0015
    'RB7 > SCL1 (bi-directional)
    SSP1CLKPPS = 0x000F
    'Module: EUSART1
    'TX1 > RB6
    RB6PPS = 0x000F
End Sub
'Template comment at the end of the config file



#DEFINE SWITCH_DOWN         0
#DEFINE SWITCH_UP           1

#DEFINE SWITCH      PORTA.5

#DEFINE USART_BAUD_RATE 19200
#DEFINE USART_TX_BLOCKING
#DEFINE sync SYNC_TX1STA

' ----- Define Hardware settings for hwi2c
' Define I2C settings - CHANGE PORTS if required for your specific device.
#DEFINE hi2c_BAUD_RATE 400
#DEFINE hi2c_DATA PORTb.5
#DEFINE hi2c_CLOCK PORTb.7
'Initialise I2C Master
'I2C pins need to be input for SSP2 module
Dir hi2c_DATA In
Dir hi2c_CLOCK In

HI2CMode Master

' ----- Main body of program commences here.
' Now assumes Serial Terminal is operational
Dim DeviceID As Byte
Dim DISPLAYNEWLINE As Byte

Do
    HSerPrintCRLF
    HSerPrint "Hardware I2C "
    HSerPrintCRLF 2

    ' Now assumes Serial Terminal is operational
    HSerPrintCRLF
    HSerPrint "   "
    'Create a horizontal row of numbers
    For DeviceID = 0 To 15
        HSerPrint Hex(deviceID)
        HSerPrint " "
    Next

    'Create a vertical column of numbers
    For DeviceID = 0 To 255
        DisplayNewLine = DeviceID % 16
        If DisplayNewLine = 0 Then
            HSerPrintCRLF
            HSerPrint Hex(DeviceID)
            If DisplayNewLine > 0 Then
                HSerPrint " "
            End If
        End If
        HSerPrint " "

        'Do an initial Start
        HI2CStart
        If HI2CWaitMSSPTimeout <> TRUE Then

            'Send to address to device
            HI2CSend ( deviceID )

            'Did device fail to respond?
            If HI2CAckpollState = FALSE Then
                HI2CSend ( 0 )
                HSerPrint   Hex(deviceID)
            Else
                HSerPrint "--"
            End If

            'Do a stop.
            HI2CStop
        Else
            HSerPrint "! "
        End If

    Next

    HSerPrintCRLF 2
    HSerPrint   "End of Search"
    HSerPrintCRLF 2
    Wait 1 s
    Wait While switch = On
Loop

' ----- Support methods.  Subroutines and Functions
