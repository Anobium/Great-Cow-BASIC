'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program searches for I2C devices and display their adresses to the attached PC terminal.
'''This program uses the hardware implementation of I2C and TWI for the Microchip and ATMEL microprocessors.  The microprocessor must have a suitable I2C or TWI module.
'''The hardware serial connections are shown in the program.
'''This program requires "DisplayDevices.h" in the same folder.
'''@author  TheoL plus works of EvanV
'''@licence GPL
'''@version 1.1a
'''@date    22.02.15
'''********************************************************************************

' ----- Configuration
#CHIP mega328p,16
#OPTION Explicit


' ----- Define Hardware settings
' Define I2C settings
#DEFINE HI2C_BAUD_RATE 400
#DEFINE HI2C_DATA PORTC.5
#DEFINE HI2C_CLOCK PORTC.4
'I2C pins need to be input for SSP module when used on Microchip device
'  Dir HI2C_DATA in
'  Dir HI2C_CLOCK in
HI2CMode Master

' THIS CONFIG OF THE SERIAL PORT WORKS WITH A  MAX232 (or equiv) THEN TO PC
' USART settings - CHANGE PORTS if required
#DEFINE USART_BAUD_RATE 9600
Dir PORTc.6 Out
Dir PORTc.7 In
#DEFINE USART_DELAY 5 ms
#DEFINE SerSendDelayms 10
#DEFINE USART_BLOCKING
Wait 500 ms



' ----- Main body of program commences here.
' Now assumes Serial Terminal is operational
Dim DeviceID As Byte
Dim DISPLAYNEWLINE As Byte


HSerPrintCRLF
HSerPrint "Hardware I2C "
HSerPrintCRLF 2

' Now assumes Serial Terminal is operational
HSerPrintCRLF
HSerPrint "   "
'Create a horizontal row of numbers
For DeviceID = 0 To 15
    HSerPrint Hex(deviceID)
    HSerPrint " "
Next

'Create a vertical column of numbers
For DeviceID = 0 To 255
    DisplayNewLine = DeviceID % 16
    If DisplayNewLine = 0 Then
        HSerPrintCRLF
        HSerPrint Hex(DeviceID)
        If DisplayNewLine > 0 Then
            HSerPrint " "
        End If
    End If
    HSerPrint " "

    'Do an initial Start
    HI2CStart
    If HI2CWaitMSSPTimeout <> TRUE Then

        'Send to address to device
        HI2CSend ( deviceID )

        'Did device fail to respond?
        If HI2CAckpollState = FALSE Then
            HSerPrint   Hex(deviceID)
            HI2CSend ( 0 )
        Else
            HSerPrint "--"
        End If

        'Do a stop.
        HI2CStop

    End If

Next

HSerPrintCRLF 2
HSerPrint   "End of Search"
HSerPrintCRLF 2


' ----- Support methods.  Subroutines and Functions
