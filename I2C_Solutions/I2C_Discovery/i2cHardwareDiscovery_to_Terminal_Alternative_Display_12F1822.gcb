'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program searches for I2C devices and display their adresses to the attached PC terminal.
'''This program uses the hardware implementation of I2C and TWI for the Microchip and ATMEL microprocessors.  The microprocessor must have a suitable I2C or TWI module.
'''The hardware serial connections are shown in the program.

'''@author  EvanV
'''@licence GPL
'''@version 1.0a
'''@date    13/2/2016
'''********************************************************************************

' ----- Configuration
#CHIP 12F1840, 32
#OPTION Explicit


' ----- Define Hardware settings
' Define I2C settings - CHANGE PORTS if required
#DEFINE HI2C_BAUD_RATE 100
#DEFINE HI2C_DATA PORTA.2
#DEFINE HI2C_CLOCK PORTA.1
'Initialise I2C Slave
'I2C pins need to be input for SSP module
Dir HI2C_DATA In
Dir HI2C_CLOCK In
'MASTER
HI2CMode Master

' THIS CONFIG OF THE SERIAL PORT WORKS WITH A  MAX232 (or equiv) THEN TO PC
' USART settings - CHANGE PORTS if required
#DEFINE USART_BAUD_RATE 9600
Dir PORTA.4 Out
Dir PORTA.5 In
#DEFINE USART_TX_BLOCKING
Wait 500 ms

' ----- Variables
Dim DISPLAYNEWLINE, HI2CWaitMSSPTimeout, HI2CAckpollState As Byte

' ----- Main body of program commences here.
' Now assumes Serial Terminal is operational
Dim DeviceID As Byte

HSerPrintCRLF
HSerPrint "Hardware I2C "
HSerPrintCRLF 2

HSerPrint "     "
For DeviceID = 0 To 15
    HSerPrint Hex(deviceID)
    HSerPrint " "
Next
For DeviceID = 0 To 255
    DisplayNewLine = DeviceID % 16
    If DisplayNewLine = 0 Then
        HSerPrintCRLF
        HSerPrint Hex(DeviceID)
        HSerPrint ": "
    End If
    HSerPrint " "
    HI2CStart
    If HI2CWaitMSSPTimeout <> TRUE Then

        HI2CSend ( deviceID )

        If HI2CAckpollState = FALSE Then
            HSerPrint   Hex(deviceID)
        Else
            HSerPrint "--"
        End If
        HI2CSend ( 0)

    End If
    HI2CStop

Next

HSerPrintCRLF 2
HSerPrint   "End of Search"
HSerPrintCRLF 2
Wait 1 s
End



' ----- Support methods.  Subroutines and Functions
