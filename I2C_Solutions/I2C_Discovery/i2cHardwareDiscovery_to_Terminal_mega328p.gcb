'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program searches for I2C devices and display their adresses to the attached PC terminal.
'''This program uses the hardware implementation of I2C and TWI for the Microchip and ATMEL microprocessors.  The microprocessor must have a suitable I2C or TWI module.
'''The hardware serial connections are shown in the program.
'''@author  TheoL plus works of EvanV
'''@licence GPL
'''@version 1.2a
'''@date    25.01.2021
'''********************************************************************************
'''
'''
/*

Example Serial display show the addresses of 0x78 and 0x79

Show Hardware I2C Bus

     00 01 02 03 04 05 06 07 08 09 0A 0B 0C 0D 0E 0F
00:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
10:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
20:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
30:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
40:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
50:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
60:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
70:  -- -- -- -- -- -- -- -- 78 79 -- -- -- -- -- --
80:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
90:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
A0:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
B0:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
C0:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
D0:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
E0:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
F0:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --

End of Search



If you get this... then, check you connnections - you have NOT connected anything, or, you are missing the pullup resistors.


    Show Hardware I2C Bus

         00 01 02 03 04 05 06 07 08 09 0A 0B 0C 0D 0E 0F
    00:

If you get this...NOTHING... but -- and you have the I2C data and clock connected... you may have them connected to the incorrect port.


    Show Hardware I2C Bus

         00 01 02 03 04 05 06 07 08 09 0A 0B 0C 0D 0E 0F
    00:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
    10:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
    20:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
    30:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
    40:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
    50:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
    60:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
    70:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
    80:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
    90:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
    A0:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
    B0:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
    C0:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
    D0:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
    E0:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --
    F0:  -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- --

    End of Search



*/



; ----- Configuration
#chip mega328p,16
#option Explicit
;Great Cow BASIC simplifies the configuration by setting the frequency and the configuration for you.

    'USART settings for USART1 on the chip which is sent via the USB to the PC
    #DEFINE USART_BAUD_RATE 9600
    #DEFINE USART_TX_BLOCKING
    #DEFINE USART_DELAY 0


    ; ----- Define Hardware settings for hwi2c
    ; Define I2C settings
    #define HI2C_BAUD_RATE 100
    #define HI2C_DATA PORTC.4
    #define HI2C_CLOCK PORTC.5
    'I2C pins need to be input for SSP module when used on Microchip device
    Dir HI2C_DATA in
    Dir HI2C_CLOCK in
    HI2CMode Master

  ; ----- Main body of program commences here.
    ' Now assumes Serial Terminal is operational
    dim DeviceID as byte
    Dim DisplayNewLine as Byte

    Do
        HSerPrintCRLF 2
        HSerPrint "Show Hardware I2C Bus"
        HSerPrintCRLF 2

        HSerPrint "     "
        for DeviceID = 0 to 15
          HSerPrint hex(deviceID)
          HSerPrint " "
        next
        for DeviceID = 0 to 255
          DisplayNewLine = DeviceID % 16
          if DisplayNewLine = 0 Then
            HSerPrintCRLF
            HserPrint hex(DeviceID)
            HSerPrint ": "
          end if
          HSerPrint " "
          HI2CStart
          if HI2CWaitMSSPTimeout <> True then

            HI2CSend ( deviceID )

            if HI2CAckPollState = false then
              HSerPrint   hex(deviceID)
              HI2CSend ( 0)
            Else
              HSerPrint "--"
            end if


          end if
          HI2CStop

        next

        HSerPrintCRLF 2
        HSerPrint   "End of Search"
        HSerPrintCRLF 2

        wait 3 s


    Loop
    END  ; ----- Support methods.  Subroutines and Functions
