'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program searches for I2C devices and display their adresses to the attached PC terminal.
'''This program uses the hardware implementation of I2C and TWI for the Microchip and ATMEL microprocessors.  The microprocessor must have a suitable I2C or TWI module.
'''The hardware serial connections are shown in the program.
'''This program requires "DisplayDevices.h" in the same folder.
'''@author  Theo Loermans plus works of EvanV
'''@licence GPL
'''@version 1.0a
'''@date    31.01.15
'''********************************************************************************

' ----- Configuration
#CHIP mega8, 8
#OPTION Explicit
#INCLUDE "displaydevices.h"

' ----- Define Hardware settings
HI2CMode Master
' This IS MANDATED!!!
#DEFINE HI2C_DATA
' 100 or 400 is valid
#DEFINE HI2C_BAUD_RATE 400

' THIS CONFIG OF THE SERIAL PORT WORKS WITH A  MAX232 (or equiv) THEN TO PC
' USART settings - CHANGE PORTS if required
#DEFINE USART_BAUD_RATE 9600
Dir PORTD.1 Out
Dir PORTD.0 In
#DEFINE USART_DELAY 10 ms
#DEFINE SerSendDelayms 10
#DEFINE USART_BLOCKING
Wait 500 ms

'----- Variables
Dim DEVICEID, TESTID As Byte

' ----- Main body of program commences here.
' Now assumes Serial Terminal is operational
HSerPrintCRLF 2
HSerPrint "Hardware I2C "
HSerPrint ChipNameStr
HSerPrintCRLF

For deviceID = 0 To 255
    HI2CStart
    If HI2CWaitMSSPTimeout <> TRUE Then

        HI2CSend ( deviceID )

        If HI2CAckpollState = FALSE Then
            HSerPrintCRLF
            HSerPrint   "ID: 0x"
            HSerPrint   Hex(deviceID)
            HSerSend 9
            testid =  deviceID | 1
            DisplayDevices
            HI2CSend ( 0 )

        End If

        HI2CStop

    End If
Next

HSerPrintCRLF
HSerPrint   "End Search"
HSerPrintCRLF 2

End


' ----- Support methods.  Subroutines and Functions
