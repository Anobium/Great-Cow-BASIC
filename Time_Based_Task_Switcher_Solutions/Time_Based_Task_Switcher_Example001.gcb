'  See the .h file for documenatation

#CHIP 16f18855, 4


'' -------------------LATA-----------------
'' Bit#:  -7---6---5---4---3---2---1---0---
'' LED:   ---------------|D5 |D4 |D3 |D2 |-
''-----------------------------------------
''
#DEFINE LEDD2 PORTA.0
#DEFINE LEDD3 PORTA.1
#DEFINE LEDD4 PORTA.2
#DEFINE LEDD5 PORTA.3
#DEFINE Potentiometer   PORTA.4

Dir     LEDD2 Out
Dir     LEDD3 Out
Dir     LEDD4 Out
Dir     LEDD5 Out
Dir   Potentiometer In




'---------------- TASK's DEFINITIONS -------------------
'------------------------------------------------
#INCLUDE "time_based_task_switcher_library001.h"

'base_time in us
#DEFINE base_time 500

#DEFINE Task1      read_input
'Task period in us
#DEFINE Task1_us   500
'Task subroutine
#DEFINE LTask1     led1
'Task will run every 1 s
#DEFINE LTask1_s   1
'Run Task inside interrupt subroutine
#DEFINE Run_LTask1

#DEFINE Task2      led2
'task will run every 100 ms
#DEFINE Task2_ms   100

'we will have the time delay for the LED read from ReadAD function
#DEFINE Task3      led3

'--------------------- MAIN -----------------------
'------------------------------------------------

'Setup the timer for 16f18855 - the key is TimerInitValue.  The TimerInitValue is calculated in the .h file.
'You to ensure the InitTimer0 is correct for your microcontroller.
'   Options are:
'
'   InitTimer0 Osc, TimerInitValue for standard 8-bit timer0's
'   InitTimer0 Osc,    TimerInitValue + TMR0_HFINTOSC   , POST0_1 for advanced 8-bit timer0's
'
' TimerInitValue is a calculated value to give you the time base specified.
InitTimer0 Osc,    TimerInitValue + TMR0_HFINTOSC   , POST0_1


Do
    Do_Task1
    Do_Task3
    Do_Task2
Loop

'------------------- TASK's SUBs ---------------------
'-----------------------------------------------------
Sub read_input
    'set LEDD4 period to the AD value
    time_Task3 = ReadAD(AN4)
End Sub

Sub led1
    LEDD2 = !LEDD2
End Sub

Sub led2
    LEDD3 = !LEDD3
End Sub

Sub led3
    LEDD4 = !LEDD4
End Sub
