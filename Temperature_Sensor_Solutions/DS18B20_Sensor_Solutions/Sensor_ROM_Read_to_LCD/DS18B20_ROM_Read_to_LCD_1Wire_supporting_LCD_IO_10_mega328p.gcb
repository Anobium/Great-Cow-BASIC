'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program reads the ROM address of a DS18B20 connected to Portc.3.  The DS18B20 must be setup to correctly.
'''This program shows an LCD being driven using an LCD I2C adapter.
'''Two types are supported as shown below:
''':    Set LCD_10 to 10 for the YwRobot LCD1602 IIC V1 or the Sainsmart LCD_PIC I2C adapter
''':    Set LCD_10 to 12 for the Ywmjkdz I2C adapter with pot bent over top of chip
'''@author 	EvanV
'''@licence	GPL
'''@version	1.0a
'''@date   	20.02.15
'''********************************************************************************

' ----- Configuration
#CHIP mega328p, 16
#OPTION Explicit
#INCLUDE <ds18b20.h>


' ----- Define Hardware settings
' Define I2C settings - CHANGE PORTS
#DEFINE I2C_MODE Master
#DEFINE I2C_DATA PORTC.4
#DEFINE I2C_CLOCK PORTC.5
#DEFINE I2C_DISABLE_INTERRUPTS On
'Optionally, you can reduce the I2C timings.
#DEFINE I2C_BIT_DELAY 0 us
#DEFINE I2C_CLOCK_DELAY 1 us
#DEFINE I2C_END_DELAY 0 us

'''Set up LCD
#DEFINE LCD_IO 10
#DEFINE LCD_I2C_Address_1 0x4E

' ----- Constants
' DS18B20 port settings - this is required
#DEFINE DQ PortC.3

' ----- Quick Command Reference:

' READ ROM [33h]
' This command can only be used when there is one slave on the bus.
' It allows the bus master to read the slave’s 64-bit ROM code without using the Search ROM procedure.
' If this command is used when there is more than one slave present on the bus, a data collision will occur when all the slaves attempt to respond at the same time.

' 64-BIT LASERED ROM CODE
' Each DS18B20 contains a unique 64–bit code (see Figure 6) stored in ROM.
' The least significant 8 bits of the ROM code contain the DS18B20’s 1-Wire family code: 28h.
' The next 48 bits contain a unique serial number.
' The most significant 8 bits contain a cyclic redundancy check (CRC) byte that is calculated from the first 56 bits of the ROM code. A detailed explanation of the CRC bits is provided in the CRC Generation section. The 64-bit ROM code and associated ROM function control logic allow the DS18B20 to operate as a 1-Wire device using the protocol detailed in the 1-Wire Bus System section.
' 64-Bit Lasered ROM Code
'
' 8-BIT CRC      48-BIT SERIAL NUMBER          8-BIT FAMILY CODE (28h)
' MSB  LSB       MSB              LSB          MSB                 LSB

'----- Variables
Dim YPOS, OLDDSDATA, XPOS, EIGHTBYTES, CLOCKS As Byte

' ----- Main body of program commences here.
'Main loop
CLS
Print "Great Cow Basic "
Locate 1 , 0
Print "DS18B20   @2015 "
Wait 3 s
CLS
Print "SHOW 64BIT LASERED"
Locate 1 , 0
Print "ROM CODE ON LCD"
Wait 3 s
CLS
' Ypos on the LCD
ypos = 0

' show 64-BIT LASERED ROM CODE on both rows of the LCD
Wait 100 ms
' Reset ds18b20
MasterRST
' Request a presence pulse
PPulse
Wait 100 us
' Send 64-BIT LASERED ROM CODE read code
OWout ReadRom
Wait 100 us
' Process and display the 64-BIT LASERED ROM CODE
OWinRom


oldDSdata = 255
Do Forever
    ' The function readtemp returns the integer value of the sensor
    DSdata = ReadTemp

    If oldDSdata <> DSdata Then
        ' Display the integer value of the sensor on the LCD
        Locate 1,0
        Print "Currently "
        Print DSdata
        Print CHR(223)+"C "
        oldDSdata = DSdata
    End If
Loop


' ----- Support methods.  Subroutines and Functions

'Required to support LCD_IO 0
Sub MySendToLCD(In MyLCDByte)

    If LCD_RS = Off Then
        HSerSend 254
    End If
    HSerSend MyLCDByte

End Sub

'The AXE133 specific commands to the LCD
Sub OneWireLCDcmd ( In LCDCMD, LCDValue )
    HSerSend LCDCMD
    HSerSend LCDValue

End Sub


Sub OWinRom
    ' XPos on the LCD
    xpos = 14
    For EightBytes = 1 To 8
        For Clocks = 1 To 8
            'The DS18s20 wants data LSB first
            Rotate RxData Right
            Dir DQ Out
            'Read time slot
            Set DQ Off
            Wait 4 us
            'Release bus for one wire transmission
            Dir DQ In
            Wait 7 us
            If DQ On  Then Set RxData.7 1
            If DQ Off Then Set RxData.7 0
            Wait 25 us
        Next
        Locate ypos , xpos
        Print Hex(RxData)
        xpos = xpos - 2
    Next
End Sub
