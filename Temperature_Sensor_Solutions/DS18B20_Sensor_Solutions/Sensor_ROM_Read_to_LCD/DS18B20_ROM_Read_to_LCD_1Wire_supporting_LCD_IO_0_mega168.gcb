'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program reads the ROM address of a DS18B20 connected to Portc.3.  The DS18B20 must be setup to correctly.
'''This program shows an LCD being driven using the GCB AXE133 emulator.
'''@author 	EvanV
'''@licence	GPL
'''@version	1.0a
'''@date   	14.02.15
'''********************************************************************************

' ----- Configuration
#CHIP mega168, 16
#OPTION Explicit
#INCLUDE <ds18b20.h>

' ----- Define Hardware settings
'Set up the Serial Port for ONEWIRE LCD
#DEFINE USART_BAUD_RATE 9600
Dir PORTD.1 Out
Dir PORTD.0 In
' A delay is required to enable to AXE133 Emulator to process
#DEFINE USART_DELAY 1 ms
#DEFINE USART_BLOCKING
Wait 500 ms



' ----- Constants
' DS18B20 port settings - this is required
#DEFINE DQ PortC.3

'Set up LCD
#DEFINE LCD_IO 0
'Redirection to support LCD_IO 0
#DEFINE LCDWriteByte MySendToLCD
#DEFINE LCD_NO_RW


'----- Variables
Dim YPOS, OLDDSDATA, XPOS, EIGHTBYTES, CLOCKS As Byte

' ----- Quick Command Reference:

' READ ROM [33h]
' This command can only be used when there is one slave on the bus.
' It allows the bus master to read the slave’s 64-bit ROM code without using the Search ROM procedure.
' If this command is used when there is more than one slave present on the bus, a data collision will occur when all the slaves attempt to respond at the same time.

' 64-BIT LASERED ROM CODE
' Each DS18B20 contains a unique 64–bit code (see Figure 6) stored in ROM.
' The least significant 8 bits of the ROM code contain the DS18B20’s 1-Wire family code: 28h.
' The next 48 bits contain a unique serial number.
' The most significant 8 bits contain a cyclic redundancy check (CRC) byte that is calculated from the first 56 bits of the ROM code. A detailed explanation of the CRC bits is provided in the CRC Generation section. The 64-bit ROM code and associated ROM function control logic allow the DS18B20 to operate as a 1-Wire device using the protocol detailed in the 1-Wire Bus System section.
' 64-Bit Lasered ROM Code
'
' 8-BIT CRC      48-BIT SERIAL NUMBER          8-BIT FAMILY CODE (28h)
' MSB  LSB       MSB              LSB          MSB                 LSB

' ----- Main body of program commences here.
'Main loop
CLS
Print "Great Cow Basic "
Locate 1 , 0
Print "DS18B20   @2015 "
Wait 3 s
CLS
Print "SHOW 64BIT LASERED"
Locate 1 , 0
Print "ROM CODE ON LCD"
Wait 3 s
CLS
' Ypos on the LCD
ypos = 0

' show 64-BIT LASERED ROM CODE on both rows of the LCD
Wait 100 ms
' Reset ds18b20
MasterRST
' Request a presence pulse
PPulse
Wait 100 us
' Send 64-BIT LASERED ROM CODE read code
OWout ReadRom
Wait 100 us
' Process and display the 64-BIT LASERED ROM CODE
OWinRom


oldDSdata = 255
Do Forever
    ' The function readtemp returns the integer value of the sensor
    DSdata = ReadTemp

    If oldDSdata <> DSdata Then
        ' Display the integer value of the sensor on the LCD
        Locate 1,0
        Print "Currently "
        Print DSdata
        Print CHR(223)+"C "
        oldDSdata = DSdata
    End If
Loop




' ----- Support methods.  Subroutines and Functions

'Required to support LCD_IO 0
Sub MySendToLCD(In MyLCDByte)

    If LCD_RS = Off Then
        HSerSend 254
    End If
    HSerSend MyLCDByte

End Sub

'The AXE133 specific commands to the LCD
Sub OneWireLCDcmd ( In LCDCMD, LCDValue )
    HSerSend LCDCMD
    HSerSend LCDValue

End Sub


Sub OWinRom
    ' XPos on the LCD
    xpos = 14
    For EightBytes = 1 To 8
        For Clocks = 1 To 8
            'The DS18s20 wants data LSB first
            Rotate RxData Right
            Dir DQ Out
            'Read time slot
            Set DQ Off
            Wait 4 us
            'Release bus for one wire transmission
            Dir DQ In
            Wait 7 us
            If DQ On  Then Set RxData.7 1
            If DQ Off Then Set RxData.7 0
            Wait 25 us
        Next
        Locate ypos , xpos
        Print Hex(RxData)
        xpos = xpos - 2
    Next
End Sub
