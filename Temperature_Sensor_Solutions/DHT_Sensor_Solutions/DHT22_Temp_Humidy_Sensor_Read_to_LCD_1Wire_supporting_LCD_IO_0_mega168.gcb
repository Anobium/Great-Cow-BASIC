'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program shows the humidity and the temperature of a DHT11 on the LCD.
'''This program shows an LCD being driven using the GCB AXE133 emulator.
'''@author 	EvanV
'''@licence	GPL
'''@version	1.0a
'''@date   	14.02.15
'''********************************************************************************

' ----- Configuration
#CHIP mega168, 16
#INCLUDE <dht.h>


' ----- Define Hardware settings
'Set up the Serial Port for ONEWIRE LCD
#DEFINE USART_BAUD_RATE 9600
Dir PORTD.1 Out
Dir PORTD.0 In
' A delay is required to enable to AXE133 Emulator to process
#DEFINE USART_DELAY 1 ms
#DEFINE USART_BLOCKING
Wait 500 ms



' ----- Constants
#DEFINE DHT_type    22
#DEFINE DHT_PIN PORTB.4

'ASCII code for degree mark
#DEFINE degree      223
'update period
#DEFINE period      2 s

'Set up LCD - Change to suit your configuration
#DEFINE LCD_IO 0
'Redirection to support LCD_IO 0
'You should delete the LCD support routines from this demonstration if you use an alternative LCD connectivity method.
#DEFINE LCDWriteByte MySendToLCD
#DEFINE LCD_NO_RW


' ----- Define Hardware settings


' ----- Variables

'required for DHT
Dim msg, whole, tenths As Byte
Dim rh, cels, fahr As Integer

' ----- Quick Command Reference:
'       The humidity and temperatures are signed integers, the error
'       indicator a byte. For the DHT11, the numerical results are
'       simply whole numbers. For the DHT22 numerical results
'       are fixed pointed numbers, with the decimal point assumed one
'       digit from the right. In other words, the values are scaled up
'       by 10. For example, a return value of 657 for the relative
'       humidity would be interpreted as 65.7%.

' ----- Main body of program commences here.


CLS
Print "Initializing..."
'let sensor stabilize
Wait period

Do
    'get current values
    readDHT(rh, cels, fahr, msg)
    CLS
    Select Case msg
        'all okay, so proceed
    Case 0:
        'print relative humidity
        Print "Humidity: "
        Print rh / 10
        Print "%"
        'print temperature in Celsius
        Locate 1,0
        Print "C:"
        Print cels / 10
        'print degree mark
        Print CHR(degree)
        Print " "
        'print temperature in Fahrenheit
        Print "F:"
        Print fahr / 10
        'print degree mark
        Print CHR(degree)
        'unit not responding
    Case 1:
        Print "No response..."
        'checksum error
    Case 2:
        Print "Bad checksum..."
    End Select
    'between readings
    Wait period
Loop
End

' ----- Support methods.  Subroutines and Functions

'Required to support LCD_IO 0
Sub MySendToLCD(In MyLCDByte)

    If LCD_RS = Off Then
        HSerSend 254
    End If
    HSerSend MyLCDByte

End Sub

'The AXE133 specific commands to the LCD
Sub OneWireLCDcmd ( In LCDCMD, LCDValue )
    HSerSend LCDCMD
    HSerSend LCDValue

End Sub
