'''A program  for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''
''' A bluetooth Temperature Pressure & Humidity  Transmitter using the BME280 sensor.
'''Feature list:
'''     BlueTooth communication is through a HC05 BT module hooked to the serial output of the 16F1705
'''     Supports the BME/BMP280 Sensor
'''
'''@author     Mike Otte
'''@licence    GPL
'''@version    1.00
'''@date       04.05.2018
'''********************************************************************************


#CHIP 16F1705,8

#INCLUDE <bme280.h>

'Generated by PIC PPS Tool for Great Cow Basic
'PPS Tool version: 0.0.5.4
'PinManager data: 2/9/2017
'
'Template comment at the start of the config file
'
#STARTUP InitPPS, 85

Sub InitPPS
    UNLOCKPPS

    'Module: EUSART
    'RC1 > RX
    RXPPS = 0x0011
    'TX > RC0
    RC0PPS = 0x0014
    'Module: MSSP
    'SDA > RC5
    RC5PPS = 0x0011
    'RC5 > SDA (bi-directional)
    SSPDATPPS = 0x0015
    'SCL > RC4
    RC4PPS = 0x0010
    'RC4 > SCL (bi-directional)
    SSPCLKPPS = 0x0014

    LOCKPPS
End Sub
'Template comment at the end of the config file

'USART settings - these MUST be set after the PPS settings
#DEFINE USART_BAUD_RATE 9600
#DEFINE USART_TX_BLOCKING



' Define I2C settings - CHANGE PORTS if required
#DEFINE HI2C_BAUD_RATE 400
#DEFINE HI2C_DATA PORTC.4
#DEFINE HI2C_CLOCK PORTC.3
'Initialise I2C Slave
'I2C pins need to be input for SSP module
Dir HI2C_DATA In
Dir HI2C_CLOCK In
'MASTER
HI2CMode Master


' ----- Variables
Dim Temp  As Integer
Dim Press, Humidity  As Long
Dim adc_P, adc_T As Long
Dim adc_H As Long
Dim Frac As Byte

Wait 1 s


Do Forever

    ' just indicates beginning of a scan
    HSerPrint ">"

    Temp = BME280_GetTemperature
    printIntWd ("  Temp C =  ", Temp)
    temp = temp *9/5+3205
    printIntWd ("  Temp F =  ", Temp)
    'HserPrint ","
    HSerPrintCRLF

    Press = BME280_GetPressure

    printwd ("  Pressure hpa = ", Press)
    Press = Press*100/3386
    printwd("     Pressure inHg = ", Press)
    'HserPrint ","
    HSerPrintCRLF

    Humidity = BME280_GetHumidity
    'HSerPrint "  adc_H = "
    'HSerPrint adc_H
    'HSerPrint "  "
    'HserPrint humidity
    printwd("     Humidity = ", Humidity/10)
    HSerPrintCRLF

    Wait 10 sec

Loop

Sub printwd (In prntstr As String, In prnval As Long)
    HSerPrint prntstr
    HSerPrint prnval/100
    HSerPrint "."
    Frac = prnval%100
    If Frac < 10 Then HSerPrint "0"
    HSerPrint Frac
End Sub

Sub printIntWd (In prntstr As String, In prnval As Integer)
    HSerPrint prntstr
    HSerPrint prnval/100
    HSerPrint "."
    Frac = prnval%100
    If Frac < 10 Then HSerPrint "0"
    HSerPrint Frac
End Sub


