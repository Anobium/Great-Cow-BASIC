'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program is a demonstration of a simple dimmer.
'''The LED array must be attached via appropiate resistors.
'''The pot on port ANO contros the value of the dimmer state.
'''An LED array of 8 LEDS is connected to PortC.
'''A single LED is connect to Portb.2 to show active state.
'''A push button is connected to PortB.1 to activate the control, this should be pulled high by a suitable resisitor.
'''@author 	EvanV plus works of HughC
'''@licence	GPL
'''@version	1.0a
'''@date   	31.01.2015
'''********************************************************************************

' ----- Configuration
#CHIP mega328p,16
#OPTION Explicit


' ----- Define Hardware settings
' ADC0
Dir portC.0 In

' Switch pulled high with 10k resistor
Dir portB.1 In

' Single LED
Dir portB.2 Out

' LED Array
Dir portC Out

' ----- Constants
#DEFINE button PORTB.1
#DEFINE LED PORTB.2

#DEFINE PulsePeriod 1 10us

'Highest value recorded from potentiometer
#DEFINE Peak 0xdd
#DEFINE GraphStep 27

' ----- Variables
Dim TT, COUNTER, CONTROLVALUE, TEMP, STOPFLICKER, TEMP3, TEMP2 As Byte
Counter = 0


' ----- Main body of program commences here.
Main:
Counter = Counter + 1
' You can use 0 [zero], or ADC0 or ANO they are mapped to the correct analog port.
'ControlValue = ReadAD( 0 )
ControlValue  = tt
tt++
If Counter > ControlValue Then Set led Off
If Counter => Peak Then Set led On: Counter = 0
Temp = ControlValue/GraphStep
BarGraph Temp
Wait PulsePeriod
If button Off Then Flicker
Goto Main

End

' ----- Support methods.  Subroutines and Functions

Sub Flicker
    Wait Until button On
    Wait 10 ms
    StopFlicker = 0

FlickerStart:
    For Temp = 1 To 100
        For Temp3 = 1 To 20
            If button Off Then Goto EndFlicker
            For Temp2 = 1 To 100
                Set led Off
                If Temp > Temp2 Then Set led On
                Wait PulsePeriod
            Next
        Next
    Next
    For Temp = 1 To 100
        For Temp3 = 1 To 20
            If button Off Then Goto EndFlicker
            For Temp2 = 1 To 100
                Set led On
                If Temp > Temp2 Then Set led Off
                Wait PulsePeriod
            Next
        Next
    Next
    Goto FlickerStart

EndFlicker:
    Wait Until button On
    Wait 10 ms
End Sub

Sub BarGraph (level)
    PORTC = 0
    If level >= 1 Then Set PORTC.0 On
    If level >= 2 Then Set PORTC.1 On
    If level >= 3 Then Set PORTC.2 On
    If level >= 4 Then Set PORTC.3 On
    If level >= 5 Then Set PORTC.4 On
    If level >= 6 Then Set PORTC.5 On
    If level >= 7 Then Set PORTC.6 On
    If level >= 8 Then Set PORTC.7 On
End Sub
