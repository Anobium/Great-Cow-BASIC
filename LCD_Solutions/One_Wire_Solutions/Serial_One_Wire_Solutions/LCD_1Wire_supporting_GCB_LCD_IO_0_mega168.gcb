'''A demonstration program for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program is a Great Cow Basic implementation of the Picaxe AXE133 Serial LCD/OLED driver.
'''    This is a OneWire LCD implementation using the LCD_IO 0.
'''    This requires the GCB AXE133 emulator but this demonstration uses the standard LCD command set plus one command to control the remote LCD.
'''    See www.picaxe.com for details of the AXE133 and the Open Source code this is based upon.
'''     Supported Commands, see LCD Wire Usage.txt
'''     0-252 normal ASCII characters
'''     253, X  display 16 character pre-saved message from EEPROM memory, X can be 0-15
'''     254, X  LCD command, X can be 0 to 255
'''     255, X  control outputs A.3, A.2, A.1, A.0 (via lower 4 bits of X)
'''     So, if using a backlit LCD with the active low transistor driver
'''     on output A.2, then 255,%000 switches backlight on and 255,%100 switches off
'''@author  EvanV
'''@licence GPL
'''@version 1.0a
'''@date    14.02.2015
'''********************************************************************************

' ----- Configuration

#CHIP mega168, 16
#OPTION Explicit

' ----- Define Hardware settings
'Set up the Serial Port
#DEFINE USART_BAUD_RATE 9600
Dir PORTD.1 Out
Dir PORTD.0 In
' A delay is required to enable to AXE133 Emulator to process
#DEFINE USART_DELAY 1 ms
#DEFINE USART_BLOCKING
Wait 500 ms

' ----- Constants
#DEFINE LCD_IO 0
'Redirection to support LCD_IO 0
#DEFINE LCDWriteByte MySendToLCD
#DEFINE LCD_NO_RW

'----- Variables
Dim DISPLAYEEPROMMESSAGE As Byte




' ----- Quick Command Reference:

'    OneWireLCDcmd = 255
'        Outputs A.3, A.2, A.1 and A.0 are all controlled by the lower 3 bits of an output byte, which is prefixed by the number 255.
'        OneWireLCDcmd 255, b'00000111'         ; all outputs on
'        wait  1 s
'        OneWireLCDcmd 255, b'00000000'         ; all outputs off

'    OneWireLCDcmd = 253.
'        Predefined messages are all prefixed by the number 253. There are up to 16 (0-15) predefined messages, all stored within the LCD firmware program.
'        To reprogram the messages simply reprogram the AXE133 emulator chip.
'
'        OneWireLCDcmd 253 , 1         ; display predefined message 1
'        wait 10 ms

' ----- Main body of program commences here.

Do Forever
    CLS
    Print "START LCD TEST"
    Locate 1, 0
    Print "DISPLAY ON"
    Wait 1 s

    For displayEEPromMessage = 0 To 15
        CLS
        Print "EEProm Msg#"
        Print displayEEPromMessage
        'Second Line
        Locate 1,0
        'Display an EEprom message
        OneWireLCDcmd 253, displayEEPromMessage
        Wait 1 s
    Next
    'All LCD outputs on
    OneWireLCDcmd 255, b'00000111'
    Wait  1 s
    'All LCD outputs off
    ' all outputs off
    OneWireLCDcmd  255, b'00000000'
    Wait  1 s

Loop

' ----- Support methods.  Subroutines and Functions

'Required to support LCD_IO 0
Sub MySendToLCD(In MyLCDByte)

    If LCD_RS = Off Then
        HSerSend 254
    End If
    HSerSend MyLCDByte

End Sub

'The AXE133 specific commands to the LCD
Sub OneWireLCDcmd ( In _LCDCmd2, _LCDValue )
    HSerSend _LCDCmd2
    HSerSend _LCDValue

End Sub
