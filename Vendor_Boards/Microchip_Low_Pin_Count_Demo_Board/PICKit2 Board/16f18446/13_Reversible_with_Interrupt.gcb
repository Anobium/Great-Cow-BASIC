'''
''' Great Cow BASIC Header block Header block
'''
'''@author  Chris Roper
'''@licence GPL
'''@version 1.00
'''@date    03/05/2018
'''********************************************************************

  #Chip 16F18446, 8
  #config MCLRE_OFF

  #option explicit

  #Define ADC_Fudge 0x0739   ' as found by A2D example

  Dim _DIRECTION as Byte

  #define SWITCH_DOWN         0
  #define SWITCH_UP           1

  #define LED_RIGHT           TRUE
  #define LED_LEFT            FAlSE

  #define SWITCH      PORTA.3

  dim mydelay as Word

  'start from the right to left
  _direction = LED_RIGHT

  dir portc out
  portc.3 = 1

  'Potentiamtor is connected to RA4...set as input
  dir porta.0 in

  'setup switch (SW1)
  dir porta.3 in

  'setup Interrupt
  IOCAN3 = 1
  On Interrupt PORTChange  Call DirChange

  do

    wait Scale( ReadAD12 ( AN0 ), 0, ADC_Fudge, 100, 1000) ms

    if  _direction = LED_RIGHT  then
      'shift to the right by 1
      Set C off
      Rotate portc right

      ' when the last LED is lit, restart the pattern
      if C = 1 then portc.3 = 1
    else
      'shift to the left by 1
      Set C off
      Rotate portc left

      'when the last LED is lit, restart the pattern
      if  portC.4 = 1 then
        portC.0 = 1
        portC.4 = 0
      end if
    end if
  loop

End ' End execution and put the device into low power sleep mode.

sub DirChange

  if IOCAF3 = 1 then
    IOCIE  = 0  ' Disable Port Change Interruts to Prevent renter of the same event
    IOCAF3 = 0  ' Ackowlage the Interrupt by Clearing this Flag
    _direction = !_direction ; Reverse direction
    IOCIE = 1  ' Reenable Port change interrupts
  end if

end sub
